unit BisCallServerSipHandler;

interface

uses Windows, Classes, ZLib, mmSystem, SyncObjs,
     IdGlobal, IdSocketHandle, IdUDPServer,
     BisEvents, BisCrypter,
     BisSipPhone, BisSipClient, BisSip, BisSdp, BisRtp, BisThreads, BisAudioWave, 
     BisCallServerHandlerModules, BisCallServerHandlers,
     BisCallServerChannels;

type
  TBisCallServerSipHandler=class;

  TBisCallServerSipChannels=class;

  TBisCallServerSipChannel=class(TBisCallServerChannel)
  private
    FHandler: TBisCallServerSipHandler;
    FLine: TBisSipPhoneLine;
    FLock: TCriticalSection;
    FInFormat: PWaveFormatEx;
    FOutFormat: PWaveFormatEx;
    FCreatorId: Variant;
    FDateCreate: TDateTime;
    FCallId: Variant;
    FCallerId: Variant;
    FCallerPhone: Variant;
    FDirection: TBisCallServerChannelDirection;
  protected
    function GetActive: Boolean; override;
    function GetDirection: TBisCallServerChannelDirection; override;
    function GetCallerPhone: Variant; override;
    function GetCallerDiversion: Variant; override;
    function GetCreatorId: Variant; override;
    function GetDateCreate: TDateTime; override;
    function GetChannelName: String; override;
    function GetInFormat: PWaveFormatEx; override;
    function GetOutFormat: PWaveFormatEx; override;
    procedure SetOutFormat(const Value: PWaveFormatEx); override;
    function GetInDataSize: Integer; override;
    function GetLocation: TBisCallServerChannelLocation; override;
    function GetVolume: Integer; override;
    function GetListenThread: TThread; override;

  public
    constructor Create(AOwner: TComponent); override;
    destructor Destroy; override;
    procedure Dial(Acceptor: Variant; AcceptorType: TBisCallServerChannelAcceptorType); override;
    procedure Answer; override;
    procedure Hangup; override;
    procedure Hold; override;
    procedure UnHold; override;
    procedure PlayStart(Stream: TStream; LoopCount: Integer=MaxInt); override;
    procedure PlayStop; override;
    procedure Send(const Data: Pointer; const DataSize: Cardinal); override;

    procedure InData(const Data: Pointer; const DataSize: Cardinal);
    procedure OutData(const Data: Pointer; const DataSize: Cardinal);

  end;

  TBisCallServerSipChannels=class(TBisCallServerChannels)
  private
    FHandler: TBisCallServerSipHandler;
    function GetItem(Index: Integer): TBisCallServerSipChannel;
  protected
     procedure DoChannelDestroy(Channel: TBisCallServerChannel); override;
  public
    function Find(Line: TBisSipPhoneLine): TBisCallServerSipChannel; reintroduce;
    function Add(Line: TBisSipPhoneLine; Direction: TBisCallServerChannelDirection; CallId: Variant): TBisCallServerSipChannel; reintroduce;
    function AddOutgoing(CallId, CallerId, CallerPhone: Variant): TBisCallServerSipChannel;
    function AddIncoming(Line: TBisSipPhoneLine): TBisCallServerSipChannel;

    property Items[Index: Integer]: TBisCallServerSipChannel read GetItem; default;
  end;

  TBisCallServerSipHandler=class(TBisCallServerHandler)
  private
    FPhone: TBisSipPhone;
    FSSendData: String;
    FSReceiveData: String;
    function GetChannels: TBisCallServerSipChannels;
    procedure PhoneSend(Phone: TBisSipPhone; Host: String; Port: Integer; Data: String);
    procedure PhoneReceive(Sender: TBisSipPhone; Host: String; Port: Integer; Data: String);
    procedure PhoneError(Sender: TBisSipPhone; const Message: String);
    procedure PhoneLineCreate(Sender: TBisSipPhone; Line: TBisSipPhoneLine);
    procedure PhoneLineDestroy(Sender: TBisSipPhone; Line: TBisSipPhoneLine);
    function PhoneLineCheck(Phone: TBisSipPhone; Line: TBisSipPhoneLine; Message: TBisSipMessage): Boolean;
    procedure PhoneLineRing(Sender: TBisSipPhone; Line: TBisSipPhoneLine);
    procedure PhoneLineConnect(Sender: TBisSipPhone; Line: TBisSipPhoneLine);
    procedure PhoneLineDisconnect(Sender: TBisSipPhone; Line: TBisSipPhoneLine);
    procedure PhoneLineInData(Sender: TBisSipPhone; Line: TBisSipPhoneLine; const Data: Pointer; const DataSize: Cardinal);
    procedure PhoneLineOutData(Sender: TBisSipPhone; Line: TBisSipPhoneLine; const Data: Pointer; const DataSize: Cardinal);
    procedure PhoneLineHold(Sender: TBisSipPhone; Line: TBisSipPhoneLine);
    procedure PhoneLinePlayBegin(Sender: TBisSipPhone; Line: TBisSipPhoneLine);
    procedure PhoneLinePlayEnd(Sender: TBisSipPhone; Line: TBisSipPhoneLine);
    procedure PhoneLineTimeout(Sender: TBisSipPhone; Line: TBisSipPhoneLine);
  protected
    function GetChannelsClass: TBisCallServerChannelsClass; override;
    function GetBusy: Boolean; override;
    function GetConnected: Boolean; override;
  public
    constructor Create(AOwner: TComponent); override;
    destructor Destroy; override;
    procedure Init; override;
    procedure Connect; override;
    procedure Disconnect; override;
    function AddOutgoingChannel(CallId,CallerId,CallerPhone: Variant): TBisCallServerChannel; override;

    property Channels: TBisCallServerSipChannels read GetChannels;

  published
    property SSendData: String read FSSendData write FSSendData;
    property SReceiveData: String read FSReceiveData write FSReceiveData;
  end;

procedure InitCallServerHandlerModule(AModule: TBisCallServerHandlerModule); stdcall;

exports
  InitCallServerHandlerModule;

implementation

uses SysUtils, Variants, DB,
     WaveUtils,
     BisCore, BisConsts, BisProvider, BisFilterGroups, BisUtils, BisConfig, BisLogger,
     BisCallServerSipHandlerConsts;

procedure InitCallServerHandlerModule(AModule: TBisCallServerHandlerModule); stdcall;
begin
  AModule.HandlerClass:=TBisCallServerSipHandler;
end;

{ TBisCallServerSipChannel }

constructor TBisCallServerSipChannel.Create(AOwner: TComponent);
begin
  inherited Create(AOwner);
  FLock:=TCriticalSection.Create;
  FCreatorId:=Core.AccountId;
  FDateCreate:=Core.ServerDate;
  FCallId:=Null;
  FCallerId:=Null;
  FCallerPhone:=Null;
  FInFormat:=nil;
  FOutFormat:=nil;
end;

destructor TBisCallServerSipChannel.Destroy;
begin
  FLine:=nil;
  FLock.Free;
  inherited Destroy;
end;

function TBisCallServerSipChannel.GetActive: Boolean;
begin
  Result:=inherited GetActive;
  if Assigned(FLine) then
    Result:=FLine.Active;
end;

function TBisCallServerSipChannel.GetCallerDiversion: Variant;
begin
  Result:=inherited GetCallerDiversion;
  if Assigned(FLine) and (FLine.Direction=ldIncoming) then
    Result:=iff(FLine.DiversionNumber<>'',FLine.DiversionNumber,Null);
end;

function TBisCallServerSipChannel.GetCallerPhone: Variant;
begin
  Result:=inherited GetCallerPhone;
  if Assigned(FLine) and (FLine.Direction=ldIncoming) then
    Result:=iff(FLine.Number<>'',FLine.Number,Null);
end;

function TBisCallServerSipChannel.GetChannelName: String;
begin
  Result:=inherited GetChannelName;
  if Assigned(FHandler) then
    Result:=Result+FHandler.FPhone.UserName;
end;

function TBisCallServerSipChannel.GetCreatorId: Variant;
begin
  Result:=FCreatorId;
end;

function TBisCallServerSipChannel.GetDateCreate: TDateTime;
begin
  Result:=FDateCreate;
end;

function TBisCallServerSipChannel.GetDirection: TBisCallServerChannelDirection;
begin
  Result:=inherited GetDirection;
  if Assigned(FLine) then begin
    case FLine.Direction of
      ldIncoming: Result:=cdIncoming;
      ldOutgoing: Result:=cdOutgoing;
    end;
  end else
    Result:=FDirection;
end;

function TBisCallServerSipChannel.GetInDataSize: Integer;
begin
  Result:=inherited GetInDataSize;
  if Assigned(FLine) then
    Result:=FLine.RemotePayloadLength;
end;

function TBisCallServerSipChannel.GetInFormat: PWaveFormatEx;
begin
  if Assigned(FLine) then
    Result:=FLine.InFormat
  else
    Result:=FInFormat;
end;

function TBisCallServerSipChannel.GetOutFormat: PWaveFormatEx;
begin
  Result:=FOutFormat;
end;

procedure TBisCallServerSipChannel.SetOutFormat(const Value: PWaveFormatEx);
begin
  FOutFormat:=Value;
end;

function TBisCallServerSipChannel.GetListenThread: TThread;
begin
  Result:=inherited GetListenThread;
  if Assigned(FLine) then ;
//    Result:=FLine.ListenThread;
end;

function TBisCallServerSipChannel.GetLocation: TBisCallServerChannelLocation;
begin
  Result:=inherited GetLocation;
  if Assigned(FHandler) then begin
    case FHandler.Location of
      hlInternal: Result:=clInternal;
      hlExternal: Result:=clExternal;
    end;
  end;
end;

function TBisCallServerSipChannel.GetVolume: Integer;
begin
  Result:=inherited GetVolume;
  if Assigned(FHandler) then
    Result:=FHandler.Volume;
end;


procedure TBisCallServerSipChannel.Dial(Acceptor: Variant; AcceptorType: TBisCallServerChannelAcceptorType);
begin
  inherited Dial(Acceptor,AcceptorType);
  if Assigned(FLine) and not VarIsNull(Acceptor) and (AcceptorType=catPhone) then
    FLine.Dial(VarToStrDef(Acceptor,''));
end;

procedure TBisCallServerSipChannel.Answer;
begin
  inherited Answer;
  if Assigned(FLine) then
    FLine.Answer;
end;

procedure TBisCallServerSipChannel.Hangup;
begin
  inherited Hangup;
  if Assigned(FLine) then
    FLine.Hangup;
end;

procedure TBisCallServerSipChannel.Hold;
begin
  inherited Hold;
  if Assigned(FLine) then
    FLine.Hold;
end;

procedure TBisCallServerSipChannel.UnHold;
begin
  inherited UnHold;
  if Assigned(FLine) then
    FLine.UnHold;
end;

procedure TBisCallServerSipChannel.PlayStart(Stream: TStream; LoopCount: Integer);
begin
  inherited PlayStart(Stream,LoopCount);
  if Assigned(FLine) then ;
//    FLine.PlayStart(Stream,LoopCount);
end;

procedure TBisCallServerSipChannel.PlayStop;
begin
  inherited PlayStop;
  if Assigned(FLine) then
    FLine.PlayStop;
end;

procedure TBisCallServerSipChannel.InData(const Data: Pointer; const DataSize: Cardinal);
begin
  DoData(cddIn,Data,DataSize);
end;

procedure TBisCallServerSipChannel.OutData(const Data: Pointer; const DataSize: Cardinal);
begin
  DoData(cddOut,Data,DataSize);
end;

procedure TBisCallServerSipChannel.Send(const Data: Pointer; const DataSize: Cardinal);
{var
  Converter: TBisWaveConverter;
  L: Integer;
  D: TBytes;}
begin
  FLock.Enter;
  try
{    if Assigned(FLine) and Assigned(FLine.OutFormat) and Assigned(OutFormat) and not FLine.Playing then begin
      Converter:=TBisWaveConverter.Create;
      try
        Converter.BeginRewrite(OutFormat);
        Converter.Write(Data^,DataSize);
        Converter.EndRewrite;
        if Converter.ConvertTo(FLine.OutFormat) then begin
          Converter.Stream.Position:=Converter.DataOffset;
          L:=Converter.Stream.Size-Converter.Stream.Position;
          SetLength(D,L);
          Converter.Stream.Position:=Converter.DataOffset;
          Converter.Stream.Read(Pointer(D)^,L);
          FLine.SendData(D,false);
        end;
      finally
        Converter.Free;
      end;
    end;}
  finally
    FLock.Leave;
  end;
end;

{ TBisCallServerSipChannels }

function TBisCallServerSipChannels.Add(Line: TBisSipPhoneLine; Direction: TBisCallServerChannelDirection; CallId: Variant): TBisCallServerSipChannel;
begin
  Result:=TBisCallServerSipChannel(AddClass(TBisCallServerSipChannel,false));
  if Assigned(Result) then begin
    Result.FCallId:=CallId;
    Result.FHandler:=FHandler;
    Result.FLine:=Line;
    Result.FDirection:=Direction;
  end;
end;

function TBisCallServerSipChannels.AddIncoming(Line: TBisSipPhoneLine): TBisCallServerSipChannel;
begin
  Result:=Add(Line,cdIncoming,Null);
  if Assigned(Result) then
    DoChannelCreate(Result);
end;

function TBisCallServerSipChannels.AddOutgoing(CallId, CallerId, CallerPhone: Variant): TBisCallServerSipChannel;
begin
  Result:=Add(FHandler.FPhone.AddLine(ldOutgoing),cdOutgoing,CallId);
  if Assigned(Result) then begin
    Result.FCallerId:=CallerId;
    Result.FCallerPhone:=CallerPhone;
    DoChannelCreate(Result);
  end;
end;

function TBisCallServerSipChannels.Find(Line: TBisSipPhoneLine): TBisCallServerSipChannel;
var
  i: Integer;
  Item: TBisCallServerSipChannel;
begin
  Result:=nil;
  for i:=0 to Count-1 do begin
    Item:=Items[i];
    if Item.FLine=Line then begin
      Result:=Item;
      exit;
    end;
  end;
end;

function TBisCallServerSipChannels.GetItem(Index: Integer): TBisCallServerSipChannel;
begin
  Result:=TBisCallServerSipChannel(inherited Items[Index]);
end;

procedure TBisCallServerSipChannels.DoChannelDestroy(Channel: TBisCallServerChannel);
begin
  if Assigned(Channel) and (Channel is TBisCallServerSipChannel) and
     Assigned(TBisCallServerSipChannel(Channel).FLine) then begin

    TBisCallServerSipChannel(Channel).FLine.Hangup;
    FHandler.FPhone.Lines.Remove(TBisCallServerSipChannel(Channel).FLine);
    TBisCallServerSipChannel(Channel).FLine:=nil;

  end;
  inherited DoChannelDestroy(Channel);
end;

{ TBisCallServerSipHandler }

constructor TBisCallServerSipHandler.Create(AOwner: TComponent);
begin
  inherited Create(AOwner);
  Channels.FHandler:=Self;

  FPhone:=TBisSipPhone.Create(nil);
  FPhone.CollectInPackets:=false;
  FPhone.CollectOutPackets:=false;
  FPhone.TransportMode:=tmUDP;
  FPhone.ThreadOptions:=AllPhoneThreadOptions;
  FPhone.HoldMode:=lhmEmulate;


  FPhone.OnSend:=PhoneSend;
  FPhone.OnReceive:=PhoneReceive;
  FPhone.OnError:=PhoneError;
  FPhone.OnLineCreate:=PhoneLineCreate;
  FPhone.OnLineDestroy:=PhoneLineDestroy;
  FPhone.OnLineCheck:=PhoneLineCheck;
  FPhone.OnLineRing:=PhoneLineRing;
  FPhone.OnLineConnect:=PhoneLineConnect;
  FPhone.OnLineDisconnect:=PhoneLineDisconnect;
  FPhone.OnLineInData:=PhoneLineInData;
  FPhone.OnLineOutData:=PhoneLineOutData;
  FPhone.OnLineHold:=PhoneLineHold;
  FPhone.OnLinePlayBegin:=PhoneLinePlayBegin;
  FPhone.OnLinePlayEnd:=PhoneLinePlayEnd;
  FPhone.OnLineTimeout:=PhoneLineTimeout;

//  FPhone.LineSweepInterval:=0;

  FSSendData:='Отправлены данные на %s:%d => %s';
  FSReceiveData:='Получены данные от %s:%d => %s';
end;

destructor TBisCallServerSipHandler.Destroy;
begin
  FPhone.Free;
  inherited Destroy;
end;

function TBisCallServerSipHandler.GetBusy: Boolean;
begin
  Result:=FPhone.Busy;
end;

function TBisCallServerSipHandler.GetConnected: Boolean;
begin
  Result:=FPhone.Connected;
end;

function TBisCallServerSipHandler.GetChannels: TBisCallServerSipChannels;
begin
  Result:=TBisCallServerSipChannels(inherited Channels);
end;

function TBisCallServerSipHandler.GetChannelsClass: TBisCallServerChannelsClass;
begin
  Result:=TBisCallServerSipChannels;
end;

procedure TBisCallServerSipHandler.Init;
begin
  inherited Init;

  with Params do begin
    FPhone.Scheme:=AsString(SParamScheme);
    FPhone.Protocol:=AsString(SParamProtocol);
    FPhone.UserName:=AsString(SParamUserName);
    FPhone.Password:=AsString(SParamPassword);
    FPhone.RemoteHost:=AsString(SParamRemoteHost);
    FPhone.RemotePort:=AsInteger(SParamRemotePort,FPhone.RemotePort);
    FPhone.LocalHost:=AsString(SParamLocalHost);
    FPhone.LocalPort:=AsInteger(SParamLocalPort,FPhone.LocalPort);
    FPhone.UserAgent:=AsString(SParamUserAgent);
    FPhone.Expires:=AsInteger(SParamExpires,FPhone.Expires);
    FPhone.MaxForwards:=AsInteger(SParamMaxForwards,FPhone.MaxForwards);
    FPhone.MaxActiveLines:=AsInteger(SParamMaxActiveLines,FPhone.MaxActiveLines);
    FPhone.KeepAliveInterval:=AsInteger(SParamKeepAliveInterval,FPhone.KeepAliveInterval);
    FPhone.UseReceived:=AsBoolean(SParamUseReceived,FPhone.UseReceived);
    FPhone.UseRport:=AsBoolean(SParamUseRport,FPhone.UseRport);
    FPhone.UseTrasnportNameInUri:=AsBoolean(SParamUseTrasnportNameInUri,FPhone.UseTrasnportNameInUri);
    FPhone.UsePortInUri:=AsBoolean(SParamUsePortInUri,FPhone.UsePortInUri);
    FPhone.UseGlobalSequence:=AsBoolean(SParamUseGlobalSequence,FPhone.UseGlobalSequence);
    FPhone.RequestRetryCount:=AsInteger(SParamRequestRetryCount,FPhone.RequestRetryCount);
    FPhone.RequestTimeOut:=AsInteger(SParamRequestTimeOut,FPhone.RequestTimeOut);
    FPhone.DialTimeout:=AsInteger(SParamDialTimeout,FPhone.DialTimeout);
    FPhone.TalkTimeout:=AsInteger(SParamTalkTimeout,FPhone.TalkTimeout);
    FPhone.RtpLocalPort:=AsInteger(SParamRtpLocalPort,FPhone.RtpLocalPort);

    FPhone.AudioDriverName:=AsString(SParamAudioDriverName,FPhone.AudioDriverName);
    FPhone.AudioFormatName:=AsString(SParamAudioFormatName,FPhone.AudioFormatName);
    FPhone.AudioChannels:=AsInteger(SParamAudioChannels,FPhone.AudioChannels);
    FPhone.AudioSamplesPerSec:=AsInteger(SParamAudioSamplesPerSec,FPhone.AudioSamplesPerSec);
    FPhone.AudioBitsPerSample:=AsInteger(SParamAudioBitsPerSample,FPhone.AudioBitsPerSample);

    FPhone.ConfirmCount:=AsInteger(SParamConfirmCount,FPhone.ConfirmCount);
    FPhone.SweepInterval:=AsInteger(SParamSweepInterval,FPhone.SweepInterval);

    FPhone.DtmfEnabled:=AsBoolean(SParamDtmfEnabled,FPhone.DtmfEnabled);
    FPhone.DtmfThreshold:=AsInteger(SParamDtmfThreshold,FPhone.DtmfThreshold);
    FPhone.DtmfTimeout:=AsInteger(SParamDtmfTimeout,FPhone.DtmfTimeout);
  end;

end;

procedure TBisCallServerSipHandler.PhoneSend(Phone: TBisSipPhone; Host: String; Port: Integer; Data: String);
var
  S: String;
begin
  S:=Data;
  if Trim(S)='' then
    S:=VisibleControlCharacters(S);
  S:=Trim(S);
  LoggerWrite(FormatEx(FSSendData,[Host,Port,S]));
end;

procedure TBisCallServerSipHandler.PhoneReceive(Sender: TBisSipPhone; Host: String; Port: Integer; Data: String);
var
  S: String;
begin
  S:=Data;
  if Trim(S)='' then
    S:=VisibleControlCharacters(S);
  S:=Trim(S);
  LoggerWrite(FormatEx(FSReceiveData,[Host,Port,S]));
end;

procedure TBisCallServerSipHandler.PhoneLineCreate(Sender: TBisSipPhone; Line: TBisSipPhoneLine);
begin
  if Assigned(Line) then begin
    case Line.Direction of
      ldIncoming: Channels.AddIncoming(Line);
      ldOutgoing: ;
    end;
  end;
end;

procedure TBisCallServerSipHandler.PhoneLineDestroy(Sender: TBisSipPhone; Line: TBisSipPhoneLine);
var
  Channel: TBisCallServerSipChannel;
begin
  Channel:=Channels.Find(Line);
  if Assigned(Channel) then begin
    Channels.Remove(Channel);
  end;
end;

procedure TBisCallServerSipHandler.PhoneError(Sender: TBisSipPhone; const Message: String);
begin
  LoggerWrite(Message,ltError);
end;

function TBisCallServerSipHandler.PhoneLineCheck(Phone: TBisSipPhone; Line: TBisSipPhoneLine; Message: TBisSipMessage): Boolean;
var
  Channel: TBisCallServerSipChannel;
begin
  Result:=false;
  Channel:=Channels.Find(Line);
  if Assigned(Channel) then begin
    case Line.Direction of
      ldIncoming: Result:=Channel.DoCheck;
      ldOutgoing: Result:=Channel.DoCheck;
    end;
  end;
end;

procedure TBisCallServerSipHandler.PhoneLineRing(Sender: TBisSipPhone; Line: TBisSipPhoneLine);
var
  Channel: TBisCallServerSipChannel;
begin
  Channel:=Channels.Find(Line);
  if Assigned(Channel) then begin
    Channel.DoRing;
  end;
end;

procedure TBisCallServerSipHandler.PhoneLineConnect(Sender: TBisSipPhone; Line: TBisSipPhoneLine);
var
  Channel: TBisCallServerSipChannel;
begin
  Channel:=Channels.Find(Line);
  if Assigned(Channel) then begin
    Channel.FInFormat:=Line.InFormat;
    Channel.DoConnect;
  end;
end;

procedure TBisCallServerSipHandler.PhoneLineDisconnect(Sender: TBisSipPhone; Line: TBisSipPhoneLine);
var
  Channel: TBisCallServerSipChannel;
begin
  Channel:=Channels.Find(Line);
  if Assigned(Channel) then begin
    Channel.DoDisconnect;
  end;
end;

procedure TBisCallServerSipHandler.PhoneLineInData(Sender: TBisSipPhone; Line: TBisSipPhoneLine;
                                                   const Data: Pointer; const DataSize: Cardinal);
var
  Channel: TBisCallServerSipChannel;
begin
  Channel:=Channels.Find(Line);
  if Assigned(Channel) then begin
    Channel.InData(Data,DataSize);
  end;
end;

procedure TBisCallServerSipHandler.PhoneLineOutData(Sender: TBisSipPhone; Line: TBisSipPhoneLine;
                                                    const Data: Pointer; const DataSize: Cardinal);
var
  Channel: TBisCallServerSipChannel;
begin
  Channel:=Channels.Find(Line);
  if Assigned(Channel) then begin
    Channel.OutData(Data,DataSize);
  end;
end;

procedure TBisCallServerSipHandler.PhoneLineHold(Sender: TBisSipPhone; Line: TBisSipPhoneLine);
var
  Channel: TBisCallServerSipChannel;
begin
  Channel:=Channels.Find(Line);
  if Assigned(Channel) then begin
    Channel.DoHold;
  end;
end;

procedure TBisCallServerSipHandler.PhoneLinePlayBegin(Sender: TBisSipPhone; Line: TBisSipPhoneLine);
var
  Channel: TBisCallServerSipChannel;
begin
  Channel:=Channels.Find(Line);
  if Assigned(Channel) then begin
    Channel.DoPlayBegin;
  end;
end;

procedure TBisCallServerSipHandler.PhoneLinePlayEnd(Sender: TBisSipPhone; Line: TBisSipPhoneLine);
var
  Channel: TBisCallServerSipChannel;
begin
  Channel:=Channels.Find(Line);
  if Assigned(Channel) then begin
    Channel.DoPlayEnd;
  end;
end;

procedure TBisCallServerSipHandler.PhoneLineTimeout(Sender: TBisSipPhone; Line: TBisSipPhoneLine);
var
  Channel: TBisCallServerSipChannel;
begin
  Channel:=Channels.Find(Line);
  if Assigned(Channel) then begin
    Channel.DoTimeout;
  end;
end;

function TBisCallServerSipHandler.AddOutgoingChannel(CallId, CallerId, CallerPhone: Variant): TBisCallServerChannel;
begin
  Result:=Channels.AddOutgoing(CallId,CallerId,CallerPhone);
end;

procedure TBisCallServerSipHandler.Connect;
begin
  inherited Connect;
  FPhone.Connect;
end;

procedure TBisCallServerSipHandler.Disconnect;
begin
  FPhone.Disconnect;
  inherited Disconnect;
end;

end.
