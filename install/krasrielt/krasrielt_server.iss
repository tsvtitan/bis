; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define AppBinDir "krasrielt_server"
#define AppBin SourcePath+"\"+AppBinDir
#define AppVersion GetFileVersion(AppBin+"\smhttp.bpl")
#define AppPublisher "NextSoft"
#define AppPublisherURL "http://www.nextsoft.ru"
#define OutputDir SourcePath+"\_output"
#define SetupIconFile ""
#define HistoryFile AppBin+"\history.rtf"

;#define DEMO

#ifdef DEMO
  #define AppName "Красриелт Сервер Демо"
  #define OutputBaseFilename "krasrielt_server_demo_"+AppVersion
  #define AppID "{{79639BA3-43AE-4F6D-8C64-2B0AC3164104}"
#else
  #define AppName "Красриелт Сервер"
  #define OutputBaseFilename "krasrielt_server_"+AppVersion
  #define AppID "{{C1C03D9D-491D-4BCD-8288-9D72BA10B835}"
#endif

[Setup]
AppName={#AppName}
AppVerName={#AppName} {#AppVersion}
AppPublisher={#AppPublisher}
AppPublisherURL={#AppPublisherURL}
AppSupportURL={#AppPublisherURL}
AppUpdatesURL={#AppPublisherURL}
DefaultDirName={pf}\{#AppPublisher}\{#AppName}
DefaultGroupName={#AppName}
OutputDir={#OutputDir}
OutputBaseFilename={#OutputBaseFilename}
Compression=lzma/ultra
SolidCompression=true
AppID={#AppID}
InternalCompressLevel=ultra
VersionInfoVersion={#AppVersion}
VersionInfoCompany={#AppPublisher}
VersionInfoDescription={#AppName} Setup
VersionInfoTextVersion={#AppVersion}
VersionInfoCopyright={#AppPublisher}
MinVersion=4.0.950,5.0.2195
AppCopyright={#AppPublisher}
AppMutex=NextSoft_{#AppBin}
InfoAfterFile={#HistoryFile}
EnableDirDoesntExistWarning=true
AppVersion={#AppVersion}
UninstallDisplayName={#AppName} {#AppVersion}
UninstallDisplayIcon={app}\loader.exe
SetupIconFile={#SetupIconFile}
UserInfoPage=true
PrivilegesRequired=admin

[Languages]
Name: russian; MessagesFile: compiler:Languages\Russian.isl

[Types]
Name: full; Description: Полная установка; Flags: iscustom

[Dirs]
Name: {app}\update

[Files]
Source: {#AppBin}\logs\test.log; DestDir: {app}\logs; Flags: ignoreversion; Components: server
Source: {#AppBin}\logs\work.log; DestDir: {app}\logs; Flags: ignoreversion; Components: server
Source: {#AppBin}\test.bis; DestDir: {app}; Flags: ignoreversion; Components: server
Source: {#AppBin}\work.bis; DestDir: {app}; Flags: ignoreversion; Components: server
Source: {#AppBin}\cmx.bpl; DestDir: {app}; Flags: replacesameversion; Components: server
Source: {#AppBin}\cmxmysql.bpl; DestDir: {app}; Flags: replacesameversion; Components: server
Source: {#AppBin}\core.bpl; DestDir: {app}; Flags: replacesameversion; Components: server
Source: {#AppBin}\dbrtl100.bpl; DestDir: {app}; Flags: replacesameversion; Components: server
Source: {#AppBin}\imimport.bpl; DestDir: {app}; Flags: replacesameversion; Components: server
Source: {#AppBin}\inet100.bpl; DestDir: {app}; Flags: replacesameversion; Components: server
Source: {#AppBin}\objects.bpl; DestDir: {app}; Flags: replacesameversion; Components: server
Source: {#AppBin}\omnet.bpl; DestDir: {app}; Flags: replacesameversion; Components: server
Source: {#AppBin}\rtl100.bpl; DestDir: {app}; Flags: replacesameversion; Components: server
Source: {#AppBin}\smhttp.bpl; DestDir: {app}; Flags: replacesameversion; Components: server
Source: {#AppBin}\smhttphd.bpl; DestDir: {app}; Flags: replacesameversion; Components: server
Source: {#AppBin}\smhttphu.bpl; DestDir: {app}; Flags: replacesameversion; Components: server
Source: {#AppBin}\vcl100.bpl; DestDir: {app}; Flags: replacesameversion; Components: server
Source: {#AppBin}\vclactnband100.bpl; DestDir: {app}; Flags: replacesameversion; Components: server
Source: {#AppBin}\vcldb100.bpl; DestDir: {app}; Flags: replacesameversion; Components: server
Source: {#AppBin}\vcliex100.bpl; DestDir: {app}; Flags: replacesameversion; Components: server
Source: {#AppBin}\vcljpg100.bpl; DestDir: {app}; Flags: replacesameversion; Components: server
Source: {#AppBin}\vclsmp100.bpl; DestDir: {app}; Flags: replacesameversion; Components: server
Source: {#AppBin}\vclx100.bpl; DestDir: {app}; Flags: replacesameversion; Components: server
Source: {#AppBin}\dbxmys30.dll; DestDir: {app}; Flags: replacesameversion; Components: server
Source: {#AppBin}\libmysql.dll; DestDir: {app}; Flags: replacesameversion; Components: server
Source: {#AppBin}\loader.exe; DestDir: {app}; Flags: replacesameversion; Components: server
Source: {#AppBin}\info.hex; DestDir: {app}; Flags: ignoreversion deleteafterinstall; Components: server
Source: {#AppBin}\import.imp; DestDir: {app}; Flags: ignoreversion; Components: import
Source: {#AppBin}\test.ini; DestDir: {app}; Flags: ignoreversion; Components: server
Source: {#AppBin}\work.ini; DestDir: {app}; Flags: ignoreversion; Components: server
Source: {#AppBin}\cmx.map; DestDir: {app}; Flags: ignoreversion; Components: server
Source: {#AppBin}\cmxmysql.map; DestDir: {app}; Flags: ignoreversion; Components: server
Source: {#AppBin}\core.map; DestDir: {app}; Flags: ignoreversion; Components: server
Source: {#AppBin}\imimport.map; DestDir: {app}; Flags: ignoreversion; Components: server
Source: {#AppBin}\loader.map; DestDir: {app}; Flags: ignoreversion; Components: server
Source: {#AppBin}\objects.map; DestDir: {app}; Flags: ignoreversion; Components: server
Source: {#AppBin}\omnet.map; DestDir: {app}; Flags: ignoreversion; Components: server
Source: {#AppBin}\smhttp.map; DestDir: {app}; Flags: ignoreversion; Components: server
Source: {#AppBin}\smhttphd.map; DestDir: {app}; Flags: ignoreversion; Components: server
Source: {#AppBin}\smhttphu.map; DestDir: {app}; Flags: ignoreversion; Components: server
Source: {#HistoryFile}; DestDir: {app}; Flags: ignoreversion; DestName: history.rtf; Components: server


[Icons]
Name: {userprograms}\{#AppPublisher}\{#AppName}\Запустить службу (Рабочая); Filename: {sys}\net; WorkingDir: {sys}; IconFilename: {app}\loader.exe; Comment: Запустить службу {#AppName} {#AppVersion} (Рабочая); IconIndex: 0; Parameters: start KrasrieltServerWork; Components: server
Name: {userprograms}\{#AppPublisher}\{#AppName}\Остановить службу (Рабочая); Filename: {sys}\net; WorkingDir: {sys}; IconFilename: {app}\loader.exe; Comment: Остановить службу {#AppName} {#AppVersion} (Рабочая); IconIndex: 0; Parameters: stop KrasrieltServerWork; Components: server
Name: {userprograms}\{#AppPublisher}\{#AppName}\Запустить вручную (Рабочая); Filename: {app}\loader.exe; WorkingDir: {sys}; IconFilename: {app}\loader.exe; Comment: Запустить вручную {#AppName} {#AppVersion} (Рабочая); IconIndex: 0; Parameters: /config work.ini; Components: server
Name: {userprograms}\{#AppPublisher}\{#AppName}\Запустить службу (Тестовая); Filename: {sys}\net; WorkingDir: {sys}; IconFilename: {app}\loader.exe; Comment: Запустить службу {#AppName} {#AppVersion} (Тестовая); IconIndex: 0; Parameters: start KrasrieltServerTest; Components: server
Name: {userprograms}\{#AppPublisher}\{#AppName}\Остановить службу (Тестовая); Filename: {sys}\net; WorkingDir: {sys}; IconFilename: {app}\loader.exe; Comment: Остановить службу {#AppName} {#AppVersion} (Тестовая); IconIndex: 0; Parameters: stop KrasrieltServerTest; Components: server
Name: {userprograms}\{#AppPublisher}\{#AppName}\Запустить вручную (Тестовая); Filename: {app}\loader.exe; WorkingDir: {sys}; IconFilename: {app}\loader.exe; Comment: Запустить вручную {#AppName} {#AppVersion} (Тестовая); IconIndex: 0; Parameters: /config test.ini; Components: server
Name: {userprograms}\{#AppPublisher}\{#AppName}\История; Filename: {app}\history.rtf; WorkingDir: {app}; IconFilename: {app}\history.rtf; Comment: История {#AppName} {#AppVersion}; Components: server
Name: {userprograms}\{#AppPublisher}\{#AppName}\Удалить; Filename: {uninstallexe}; WorkingDir: {app}; IconFilename: {uninstallexe}; Comment: Удалить {#AppName} {#AppVersion}; Components: server

[Components]
Name: server; Description: Программа {#AppName} {#AppVersion}; Types: full; Flags: fixed
Name: import; Description: Обновление базы данных; Types: full

[Run]
Filename: {sys}\net; Parameters: start KrasrieltServerWork; WorkingDir: {sys}; Flags: postinstall; Components: server; Description: Запустить службу {#AppName} (Рабочая)
Filename: {sys}\net; Parameters: start KrasrieltServerTest; WorkingDir: {sys}; Flags: postinstall; Components: server; Description: Запустить службу {#AppName} (Тестовая)

[UninstallRun]
Filename: {sys}\net; Parameters: stop KrasrieltServerWork; WorkingDir: {sys}; Flags: waituntilidle; Components: server
Filename: {sys}\net; Parameters: stop KrasrieltServerTest; WorkingDir: {sys}; Flags: waituntilidle; Components: server
Filename: {app}\loader.exe; Parameters: /service /uninstall /config work.ini; WorkingDir: {app}; Flags: runhidden waituntilidle; Components: server
Filename: {app}\loader.exe; Parameters: /service /uninstall /config test.ini; WorkingDir: {app}; Flags: runhidden waituntilidle; Components: server

[Code]

procedure CurStepChanged(CurStep: TSetupStep);
var
  ResultCode: Integer;
begin
  case CurStep of
    ssPostInstall: begin

      Exec(ExpandConstant('{app}\loader.exe'),'/info info.hex',ExpandConstant('{app}'),SW_HIDE,ewWaitUntilTerminated,ResultCode);
//      Exec(ExpandConstant('{app}\loader.exe'),'/config nologin.ini /command connection default',ExpandConstant('{app}'),SW_SHOWNORMAL,ewWaitUntilTerminated,ResultCode);
//      Exec(ExpandConstant('{app}\loader.exe'),'/config nologin.ini /command import import.imp',ExpandConstant('{app}'),SW_SHOWNORMAL,ewWaitUntilTerminated,ResultCode);
      Exec(ExpandConstant('{app}\loader.exe'),'/service /install /config work.ini',ExpandConstant('{app}'),SW_HIDE,ewWaitUntilTerminated,ResultCode);
      Exec(ExpandConstant('{app}\loader.exe'),'/service /install /config test.ini',ExpandConstant('{app}'),SW_HIDE,ewWaitUntilTerminated,ResultCode);

      BringToFrontAndRestore();
    end;
  end;
end;


