unit BisTaxiDataScoresFm;

interface

uses
  Windows, Messages, SysUtils, Variants, Classes, Graphics, Controls, Forms,
  Dialogs, StdCtrls, ExtCtrls, ComCtrls, BisFm, ActnList, DB,
  VirtualTrees,
  BisDataFrm, BisDataGridFrm, BisDataGridFm, BisDataEditFm, BisFieldNames;

type
  TBisTaxiDataScoresFrame=class(TBisDataGridFrame)
  private
    FDriverId: Variant;
    FDriverUserName: String;
    FDriverPatronymic: Variant;
    FDriverName: Variant;
    FDriverSurname: Variant;
    procedure GridPaintText(Sender: TBaseVirtualTree; const TargetCanvas: TCanvas;
                            Node: PVirtualNode; Column: TColumnIndex; TextType: TVSTTextType);
  protected
    procedure BeforeIfaceExecute(AIface: TBisDataEditFormIface); override;
  public
    constructor Create(AOwner: TComponent); override;

    property DriverId: Variant read FDriverId write FDriverId;
    property DriverUserName: String read FDriverUserName write FDriverUserName;
    property DriverSurname: Variant read FDriverSurname write FDriverSurname;
    property DriverName: Variant read FDriverName write FDriverName;
    property DriverPatronymic: Variant read FDriverPatronymic write FDriverPatronymic;
  end;

  TBisTaxiDataScoresForm = class(TBisDataGridForm)
  protected
    class function GetDataFrameClass: TBisDataFrameClass; override;
  end;

  TBisTaxiDataScoresFormIface=class(TBisDataGridFormIface)
  public
    constructor Create(AOwner: TComponent); override;
  end;

var
  BisTaxiDataScoresForm: TBisTaxiDataScoresForm;

implementation

uses BisUtils, BisTaxiDataScoreEditFm, BisConsts, BisParam;

{$R *.dfm}

{ TBisTaxiDataScoresFrame }

constructor TBisTaxiDataScoresFrame.Create(AOwner: TComponent);
begin
  inherited Create(AOwner);
  FilterClass:=TBisTaxiDataScoreFilterFormIface;
  InsertClass:=TBisTaxiDataScoreInsertFormIface;
  UpdateClass:=TBisTaxiDataScoreUpdateFormIface;
  DeleteClass:=TBisTaxiDataScoreDeleteFormIface;
  with Provider do begin
    ProviderName:='S_SCORES';
    with FieldNames do begin
      AddKey('SCORE_ID');
      AddInvisible('DRIVER_ID');
      AddInvisible('RATING_ID');
      AddInvisible('CREATOR_ID');
      AddInvisible('CREATOR_USER_NAME');
      AddInvisible('DESCRIPTION');
      Add('DATE_CREATE','Дата создания',110);
      Add('DRIVER_USER_NAME','Водитель',110);
      Add('RATING_NAME','Вид оценки',150);
      Add('AMOUNT','Баллы',50);
    end;
  end;

  Grid.OnPaintText:=GridPaintText;
  
  FDriverId:=Null;
end;

procedure TBisTaxiDataScoresFrame.GridPaintText(Sender: TBaseVirtualTree; const TargetCanvas: TCanvas;
                                                       Node: PVirtualNode; Column: TColumnIndex; TextType: TVSTTextType);
var
  Amount: Variant;
  V: Integer;
begin
  if (Node<>Sender.FocusedNode) or ((Node=Sender.FocusedNode) and (Column<>Sender.FocusedColumn)) then begin
    Amount:=Grid.GetNodeValue(Node,'AMOUNT');
    if not VarIsNull(Amount) then begin
      V:=VarToIntDef(Amount,0);
      if V>0 then
        TargetCanvas.Font.Color:=clGreen
      else if V<0 then
        TargetCanvas.Font.Color:=clRed;
    end;
  end;
end;

procedure TBisTaxiDataScoresFrame.BeforeIfaceExecute(AIface: TBisDataEditFormIface);
var
  Param: TBisParam;
begin
  inherited BeforeIfaceExecute(AIface);
  if Assigned(AIface) then begin
    with AIface.Params do begin
      ParamByName('DRIVER_USER_NAME').Value:=FDriverUserName;
      Param:=ParamByName('DRIVER_ID');
      Param.Value:=FDriverId;
      if not Param.Empty then
        Param.ExcludeModes(AllParamEditModes);
    end;
  end;
end;

{ TBisTaxiDataScoresFormIface }

constructor TBisTaxiDataScoresFormIface.Create(AOwner: TComponent);
begin
  inherited Create(AOwner);
  FormClass:=TBisTaxiDataScoresForm;
  Permissions.Enabled:=true;
  ChangeFrameProperties:=false;
end;

{ TBisTaxiDataScoresForm }

class function TBisTaxiDataScoresForm.GetDataFrameClass: TBisDataFrameClass;
begin
  Result:=TBisTaxiDataScoresFrame;
end;

end.
