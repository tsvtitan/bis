; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT!

#define AppBinDir "docpro"
#define AppBin SourcePath+"\"+AppBinDir
#define AppVersion GetFileVersion(AppBin+"\imdocpro.bpl")
#define AppPublisher "NextSoft"
#define AppPublisherURL "http://www.nextsoft.ru"
#define OutputDir SourcePath+"\_output"
#define SetupIconFile ""
#define HistoryFile AppBin+"\history.rtf"

;#define DEMO

#ifdef DEMO
  #define AppName "Документооборот Демо"
  #define OutputBaseFilename "docpro_demo_"+AppVersion
  #define AppID "{{EFD71F8C-A66E-4A26-BD78-08367D9471A7}"
#else
  #define AppName "Документооборот"
  #define OutputBaseFilename "docpro_"+AppVersion
  #define AppID "{{CCB0FD9A-FA66-4815-8953-B16B404E56F4}"
#endif

[Setup]
AppName={#AppName}
AppVerName={#AppName} {#AppVersion}
AppPublisher={#AppPublisher}
AppPublisherURL={#AppPublisherURL}
AppSupportURL={#AppPublisherURL}
AppUpdatesURL={#AppPublisherURL}
DefaultDirName={pf}\{#AppPublisher}\{#AppName}
DefaultGroupName={#AppName}
OutputDir={#OutputDir}
OutputBaseFilename={#OutputBaseFilename}
Compression=lzma/ultra
SolidCompression=true
AppID={#AppID}
InternalCompressLevel=ultra
VersionInfoVersion={#AppVersion}
VersionInfoCompany={#AppPublisher}
VersionInfoDescription={#AppName} Setup
VersionInfoTextVersion={#AppVersion}
VersionInfoCopyright={#AppPublisher}
MinVersion=4.0.950,5.0.2195
AppCopyright={#AppPublisher}
AppMutex=NextSoft_docpro
InfoAfterFile={#HistoryFile}
EnableDirDoesntExistWarning=true
AppVersion={#AppVersion}
UninstallDisplayName={#AppName} {#AppVersion}
UninstallDisplayIcon={app}\icon.ico
SetupIconFile={#SetupIconFile}
UserInfoPage=true
PrivilegesRequired=admin

[Languages]
Name: russian; MessagesFile: compiler:Languages\Russian.isl

[Types]
Name: full; Description: Полная установка; Flags: iscustom

[Files]
Source: {#AppBin}\client.bis; DestDir: {app}; Flags: ignoreversion; Components: client
Source: {#AppBin}\cmx.bpl; DestDir: {app}; Flags: replacesameversion; Components: client
Source: {#AppBin}\cmxmssql.bpl; DestDir: {app}; Flags: replacesameversion; Components: client
Source: {#AppBin}\core.bpl; DestDir: {app}; Flags: replacesameversion; Components: client
Source: {#AppBin}\dbexpress4100.bpl; DestDir: {app}; Flags: replacesameversion; Components: client
Source: {#AppBin}\dbrtl100.bpl; DestDir: {app}; Flags: replacesameversion; Components: client
Source: {#AppBin}\DbxClientDriver100.bpl; DestDir: {app}; Flags: replacesameversion; Components: client
Source: {#AppBin}\DbxCommonDriver100.bpl; DestDir: {app}; Flags: replacesameversion; Components: client
Source: {#AppBin}\DbxDynalinkDriver100.bpl; DestDir: {app}; Flags: replacesameversion; Components: client
Source: {#AppBin}\DbxReadOnlyMetaData100.bpl; DestDir: {app}; Flags: replacesameversion; Components: client
Source: {#AppBin}\imdesign.bpl; DestDir: {app}; Flags: replacesameversion; Components: client
Source: {#AppBin}\imdocpro.bpl; DestDir: {app}; Flags: replacesameversion; Components: client
Source: {#AppBin}\imimport.bpl; DestDir: {app}; Flags: replacesameversion; Components: import
Source: {#AppBin}\objects.bpl; DestDir: {app}; Flags: replacesameversion; Components: client
Source: {#AppBin}\rtl100.bpl; DestDir: {app}; Flags: replacesameversion; Components: client
Source: {#AppBin}\tee7100.bpl; DestDir: {app}; Flags: replacesameversion; Components: client
Source: {#AppBin}\teedb7100.bpl; DestDir: {app}; Flags: replacesameversion; Components: client
Source: {#AppBin}\teeui7100.bpl; DestDir: {app}; Flags: replacesameversion; Components: client
Source: {#AppBin}\vcl100.bpl; DestDir: {app}; Flags: replacesameversion; Components: client
Source: {#AppBin}\vclactnband100.bpl; DestDir: {app}; Flags: replacesameversion; Components: client
Source: {#AppBin}\vcldb100.bpl; DestDir: {app}; Flags: replacesameversion; Components: client
Source: {#AppBin}\vcliex100.bpl; DestDir: {app}; Flags: replacesameversion; Components: client
Source: {#AppBin}\vcljpg100.bpl; DestDir: {app}; Flags: replacesameversion; Components: client
Source: {#AppBin}\vclsmp100.bpl; DestDir: {app}; Flags: replacesameversion; Components: client
Source: {#AppBin}\vclx100.bpl; DestDir: {app}; Flags: replacesameversion; Components: client
Source: {#AppBin}\dbxmss30.dll; DestDir: {app}; Flags: replacesameversion; Components: client
Source: {#AppBin}\loader.exe; DestDir: {app}; Flags: replacesameversion; Components: client
Source: {#AppBin}\info.hex; DestDir: {app}; Flags: ignoreversion deleteafterinstall; Components: client
Source: {#AppBin}\icon.ico; DestDir: {app}; Flags: ignoreversion; Components: client
Source: {#AppBin}\import.imp; DestDir: {app}; Flags: ignoreversion; Components: import
Source: {#AppBin}\client.ini; DestDir: {app}; Flags: ignoreversion; Components: client
Source: {#AppBin}\client.log; DestDir: {app}; Flags: ignoreversion; Components: client
Source: {#AppBin}\cmx.map; DestDir: {app}; Flags: ignoreversion; Components: client
Source: {#AppBin}\cmxmssql.map; DestDir: {app}; Flags: ignoreversion; Components: client
Source: {#AppBin}\core.map; DestDir: {app}; Flags: ignoreversion; Components: client
Source: {#AppBin}\imdesign.map; DestDir: {app}; Flags: ignoreversion; Components: client
Source: {#AppBin}\imdocpro.map; DestDir: {app}; Flags: ignoreversion; Components: client
Source: {#AppBin}\imimport.map; DestDir: {app}; Flags: ignoreversion; Components: client
Source: {#AppBin}\loader.map; DestDir: {app}; Flags: ignoreversion; Components: client
Source: {#AppBin}\objects.map; DestDir: {app}; Flags: ignoreversion; Components: client
Source: {#HistoryFile}; DestDir: {app}; Flags: ignoreversion; DestName: history.rtf; Components: client

[Icons]
Name: {userprograms}\{#AppPublisher}\{#AppName}\Запустить; Filename: {app}\loader.exe; WorkingDir: {app}; IconFilename: {app}\icon.ico; Comment: Запустить {#AppName} {#AppVersion}; Parameters: /config client.ini; Flags: runmaximized; Components: client
Name: {userprograms}\{#AppPublisher}\{#AppName}\История; Filename: {app}\history.rtf; WorkingDir: {app}; IconFilename: {app}\history.rtf; Comment: История {#AppName} {#AppVersion}; Components: client
Name: {userprograms}\{#AppPublisher}\{#AppName}\Удалить; Filename: {uninstallexe}; WorkingDir: {app}; IconFilename: {uninstallexe}; Comment: Удалить {#AppName} {#AppVersion}; Components: client
Name: {userdesktop}\{#AppName}; Filename: {app}\loader.exe; WorkingDir: {app}; IconFilename: {app}\icon.ico; Comment: {#AppName} {#AppVersion}; Parameters: /config client.ini; Flags: runmaximized; Components: client

[Components]
Name: client; Description: Программа {#AppName} {#AppVersion}; Types: full; Flags: fixed
Name: import; Description: Скрипты к базе данных; Types: full

[Run]
;Filename: {app}\loader.exe; Parameters: /info info.hex; WorkingDir: {app}; Flags: runhidden waituntilidle; Components: client
;Filename: {app}\loader.exe; Parameters: /config client.ini /command connection default; WorkingDir: {app}; Flags: waituntilidle; Components: client
;Filename: {app}\loader.exe; Parameters: /config client.ini /command import import.imp; WorkingDir: {app}; Flags: waituntilidle; Components: import
Filename: {app}\loader.exe; Parameters: /config client.ini; WorkingDir: {app}; Flags: nowait postinstall skipifsilent runmaximized; Components: client; Description: Запустить {#AppName}

[UninstallRun]

[Dirs]

[Code]

procedure CurStepChanged(CurStep: TSetupStep);
var
  ResultCode: Integer;
begin
  case CurStep of
    ssPostInstall: begin

      Exec(ExpandConstant('{app}\loader.exe'),'/info info.hex',ExpandConstant('{app}'),SW_HIDE,ewWaitUntilTerminated,ResultCode);
      Exec(ExpandConstant('{app}\loader.exe'),'/config client.ini /command connection default',ExpandConstant('{app}'),SW_SHOWNORMAL,ewWaitUntilTerminated,ResultCode);

      if WizardForm.ComponentsList.Checked[1] then begin
        Exec(ExpandConstant('{app}\loader.exe'),'/config client.ini /command import import.imp',ExpandConstant('{app}'),SW_SHOWNORMAL,ewWaitUntilTerminated,ResultCode);
      end;

      BringToFrontAndRestore();
    end;
  end;
end;


