; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT files!

#define AppBinDir "ftpclient"
#define AppBin SourcePath+"\"+AppBinDir
#define WizardImage AppBin+"\wizard.bmp";
#define AppVersion GetFileVersion(AppBin+"\imftpclt.bpl")
#define AppPublisher "NextSoft"
#define AppPublisherURL "http://www.nextsoft.ru"
#define OutputDir SourcePath+"\_output"
#define SetupIconFile ""

#define AppName "Ftp-клиент Банеров"
#define OutputBaseFilename "ftpclient_"+AppVersion
#define AppID "{{F1420143-004B-42A8-B053-A538985EF745}"

[Setup]
AppName={#AppName}
AppVerName={#AppName} {#AppVersion}
AppPublisher={#AppPublisher}
AppPublisherURL={#AppPublisherURL}
AppSupportURL={#AppPublisherURL}
AppUpdatesURL={#AppPublisherURL}
DefaultDirName={pf}\{#AppPublisher}\{#AppName}
DefaultGroupName={#AppName}
OutputDir={#OutputDir}
OutputBaseFilename={#OutputBaseFilename}
Compression=lzma/ultra
SolidCompression=true
AppID={#AppID}
InternalCompressLevel=ultra
VersionInfoVersion={#AppVersion}
VersionInfoCompany={#AppPublisher}
VersionInfoDescription={#AppName} Setup
VersionInfoTextVersion={#AppVersion}
VersionInfoCopyright={#AppPublisher}
MinVersion=4.0.950,5.0.2195
AppCopyright={#AppPublisher}
AppMutex=NextSoft_FtpClient
EnableDirDoesntExistWarning=true
AppVersion={#AppVersion}
UninstallDisplayName={#AppName} {#AppVersion}
UninstallDisplayIcon={app}\ftpclt.exe
SetupIconFile={#SetupIconFile}
UserInfoPage=false
PrivilegesRequired=admin
WizardImageFile={#WizardImage}
AlwaysShowComponentsList=false

[Languages]
Name: russian; MessagesFile: compiler:Languages\Russian.isl

[Types]
;Name: full; Description: Полная установка;

[Files]
Source: {#AppBin}\ftpclt.bis; DestDir: {app}; Flags: ignoreversion
Source: {#AppBin}\core.bpl; DestDir: {app}; Flags: replacesameversion
Source: {#AppBin}\dbrtl100.bpl; DestDir: {app}; Flags: replacesameversion
Source: {#AppBin}\imftpclt.bpl; DestDir: {app}; Flags: replacesameversion
Source: {#AppBin}\objects.bpl; DestDir: {app}; Flags: replacesameversion
Source: {#AppBin}\omnet.bpl; DestDir: {app}; Flags: replacesameversion
Source: {#AppBin}\rtl100.bpl; DestDir: {app}; Flags: replacesameversion
Source: {#AppBin}\vcl100.bpl; DestDir: {app}; Flags: replacesameversion
Source: {#AppBin}\vclactnband100.bpl; DestDir: {app}; Flags: replacesameversion
Source: {#AppBin}\vcldb100.bpl; DestDir: {app}; Flags: replacesameversion
Source: {#AppBin}\vcliex100.bpl; DestDir: {app}; Flags: replacesameversion
Source: {#AppBin}\vcljpg100.bpl; DestDir: {app}; Flags: replacesameversion
Source: {#AppBin}\vclsmp100.bpl; DestDir: {app}; Flags: replacesameversion
Source: {#AppBin}\vclx100.bpl; DestDir: {app}; Flags: replacesameversion
Source: {#AppBin}\csftpclt.dll; DestDir: {app}; Flags: replacesameversion
Source: {#AppBin}\ftpclt.exe; DestDir: {app}; Flags: replacesameversion
Source: {#AppBin}\info.hex; DestDir: {app}; Flags: ignoreversion deleteafterinstall
Source: {#AppBin}\config.ini; DestDir: {app}; Flags: ignoreversion
Source: {#AppBin}\list.txt; DestDir: {app}; Flags: ignoreversion
Source: {#AppBin}\profile.txt; DestDir: {app}; Flags: ignoreversion

[Icons]
Name: {userprograms}\{#AppPublisher}\{#AppName}\Запустить; Filename: {app}\ftpclt.exe; WorkingDir: {app}; IconFilename: {app}\ftpclt.exe; Comment: Запустить {#AppName} {#AppVersion}; IconIndex: 0
Name: {userprograms}\{#AppPublisher}\{#AppName}\Удалить; Filename: {uninstallexe}; WorkingDir: {app}; IconFilename: {uninstallexe}; Comment: Удалить {#AppName} {#AppVersion}
Name: {userdesktop}\{#AppName}; Filename: {app}\ftpclt.exe; WorkingDir: {app}; IconFilename: {app}\ftpclt.exe; Comment: {#AppName} {#AppVersion}; IconIndex: 0

[Components]
;Name: client; Description: Программа {#AppName} {#AppVersion}; Types: full; Flags: fixed

[Run]
Filename: {app}\ftpclt.exe; Parameters: /info info.hex; WorkingDir: {app}; Flags: runhidden waituntilidle
Filename: {sys}\regsvr32.exe; Parameters: """{app}\csftpclt.dll"" /s"; WorkingDir: {app}; Flags: runhidden waituntilidle
Filename: {app}\ftpclt.exe; WorkingDir: {app}; Flags: nowait postinstall skipifsilent; Description: Запустить {#AppName}

[Dirs]

[UninstallRun]
Filename: {sys}\regsvr32.exe; Parameters: """{app}\csftpclt.dll"" /u /s"; WorkingDir: {app}; Flags: runhidden waituntilidle

[Code]
var
  PageParams: TWizardPage;
  EditCompany: TEdit;
  EditEmail: TEdit;

function PageParamsCreatePage(PreviousPageId: Integer): Integer;
var
  LabelInfo: TLabel;
  LabelCompany: TLabel;
  LabelEmail: TLabel;
begin
  PageParams := CreateCustomPage(PreviousPageId,
                                 'Регистрация персональных данных',
                                 'Пожалуйста, введите Ваши данные.');

  LabelInfo := TLabel.Create(PageParams);
  with LabelInfo do
  begin
    Parent := PageParams.Surface;
    Caption :='   Для регистрации программы {#AppName}, необходимо указать название компании и электронный почтовый ящик (email).';
    Left := ScaleX(0);
    Top := ScaleY(8);
    Width := ScaleX(400);
    Height := ScaleY(55);
    AutoSize := False;
    WordWrap := True;
  end;

  LabelCompany := TLabel.Create(PageParams);
  with LabelCompany do
  begin
    Parent := PageParams.Surface;
    Caption :='Название компании:';
    Left := ScaleX(23);
    Top := ScaleY(90);
    Width := ScaleX(115);
    Height := ScaleY(13);
    Alignment := taRightJustify;
  end;

  EditCompany := TEdit.Create(PageParams);
  with EditCompany do
  begin
    Parent := PageParams.Surface;
    Left := ScaleX(146);
    Top := ScaleY(86);
    Width := ScaleX(250);
    Height := ScaleY(21);
    MaxLength:=100;
    TabOrder := 1;
  end;

  LabelEmail := TLabel.Create(PageParams);
  with LabelEmail do
  begin
    Parent := PageParams.Surface;
    Caption :='Электронный ящик:';
    Left := ScaleX(23);
    Top := ScaleY(130);
    Width := ScaleX(115);
    Height := ScaleY(13);
    Alignment := taRightJustify;
  end;

  EditEmail := TEdit.Create(PageParams);
  with EditEmail do
  begin
    Parent := PageParams.Surface;
    Left := ScaleX(146);
    Top := ScaleY(126);
    Width := ScaleX(250);
    Height := ScaleY(21);
    MaxLength:=50;
    TabOrder := 1;
  end;

  Result := PageParams.ID;
end;

procedure InitializeWizard();
begin
  PageParamsCreatePage(wpSelectComponents);
end;

function CheckParams: Boolean;
begin
  Result:=(Trim(EditCompany.Text)<>'') and (Trim(EditEmail.Text)<>'');
end;

function NextButtonClick(CurPageID: Integer): Boolean;
begin
  Result:=true;
  if CurPageID=PageParams.ID then begin
    Result:=CheckParams;
    if not Result then begin
      MsgBox('Введите необходимые данные.',mbError,MB_OK);
    end;
  end;
end;

procedure CurStepChanged(CurStep: TSetupStep);
var
  IniFile: String;
begin
  case CurStep of
    ssPostInstall: begin

      IniFile:=ExpandConstant('{app}\config.ini');
      if FileExists(IniFile) then begin

        SetIniString('FtpClientMainForm','CompanyName',EditCompany.Text,IniFile);
        SetIniString('FtpClientMainForm','ServerPassword',EditEmail.Text,IniFile);

      end;
      BringToFrontAndRestore();
    end;
  end;
end;

